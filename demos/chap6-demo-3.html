<!DOCTYPE html>
<html lang="zh-CN">
<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
<title>Phong Shading</title>
<head>
<script type="text/javascript" src="../js/common/webgl-utils.js"></script>
<script type="text/javascript" src="../js/common/initShaders.js"></script>
<script type="text/javascript" src="../js/common/gl-matrix.js"></script>

<script type="text/javascript" src="../js/ch06/renderSphere.js"></script>

<script type="text/javascript">

	var renderer = 0;

	function run(){
		var vertSrc = document.getElementById( "code_vert" ).value;
		var fragSrc = document.getElementById( "code_frag" ).value;

		renderer = new Renderer( "gl-canvas", vertSrc, fragSrc );
		renderer.init();
		renderer.display();
	}

	function updateRenderer(){
		var vertSrc = document.getElementById( "code_vert" ).value;
		var fragSrc = document.getElementById( "code_frag" ).value;

		renderer.updateShader( vertSrc, fragSrc );
		renderer.display();
	}

	var interval = setInterval( timerFunc, 30 );

	function timerFunc(){
		renderer.display();
	}

	function exampleChanged(){
		var d = parseInt( document.getElementById( "select_shade_id" ).value );
		renderer.modeVal = d;
		switch( d ){
			default:
			case 1:
				document.getElementById( "code_vert" ).value = document.getElementById( "vshader-1" ).value;
				document.getElementById( "code_frag" ).value = document.getElementById( "fshader-1" ).value;
				break;
			case 2:
				document.getElementById( "code_vert" ).value = document.getElementById( "vshader-2" ).value;
				document.getElementById( "code_frag" ).value = document.getElementById( "fshader-2" ).value;
				break;
			case 3:
				document.getElementById( "code_vert" ).value = document.getElementById( "vshader-3" ).value;
				document.getElementById( "code_frag" ).value = document.getElementById( "fshader-3" ).value;
				break;
			case 4:
				document.getElementById( "code_vert" ).value = document.getElementById( "vshader-4" ).value;
				document.getElementById( "code_frag" ).value = document.getElementById( "fshader-4" ).value;
				break;
			case 5:
				document.getElementById( "code_vert" ).value = document.getElementById( "vshader-5" ).value;
				document.getElementById( "code_frag" ).value = document.getElementById( "fshader-5" ).value;
				break;
			case 6:
				document.getElementById( "code_vert" ).value = document.getElementById( "vshader-6" ).value;
				document.getElementById( "code_frag" ).value = document.getElementById( "fshader-6" ).value;
				break;
		}
		updateRenderer();
	}

</script>
</head>
<body onload="run()">
	<h1>渲染一个球</h1>
	<p style="text-align:left">几种不同着色方法的比较：</p>
	<ul>
		<li style="text-align:left">平面法线+顶点着色(半路向量)</li>
		<li style="text-align:left">平面法线+面片着色</li>
		<li style="text-align:left">顶点法线+顶点着色</li>
		<li style="text-align:left">顶点法线+面片着色</li>
		<li style="text-align:left">Gouraud着色(平面法线+顶点着色)</li>
		<li style="text-align:left">Phong着色(平面法线+面片着色)</li>
	</ul>

	<table style="border:1px solid black">
		<caption align="bottom">按键设置</caption>
		<tr>
			<td>a</td>
			<td>d</td>
			<td>w</td>
			<td>s</td>
			<td>z</td>
			<td>x</td>
		</tr>
		<tr>
			<td>增加theta</td>
			<td>减少theta</td>
			<td>增加phi</td>
			<td>减少phi</td>
			<td>增大半径radius</td>
			<td>减少半径radius</td>
		</tr>
		<tr>
			<td>v</td>
			<td>b</td>
		</tr>
		<tr>
			<td>增加分块</td>
			<td>减小分块</td>
		</tr>
	</table>

	<table>
		<tr style="vertical-align:top;">
		<td>
			<canvas id="gl-canvas" width="640" height="480">您的浏览器不支持HTML5 Canvas元素</canvas><br>
			<select onchange="exampleChanged()" id="select_shade_id">
				<option value="1" selected="selected">true normals and per vertex shading</option>
				<option value="2">true normals and per fragment shading</option>
				<option value="3">vertex normals and per vertex shading</option>
				<option value="4">vertex normals and per fragment shading</option>
				<option value="5">Gouraud Shading</option>
				<option value="6">Phong Shading</option>
			</select>
		</td>
		</tr>
		<table border="0" class="maintable" cellpadding="5px" cellspacing="5px">
	    <tr style="vertical-align: top;">
		<td>Vertex shader: <br><textarea cols="50" rows="20" class="code_input_vert" id="code_vert" wrap="logical">
attribute vec4 vPosition;
attribute vec4 vNormal;

varying vec4 fColor;

uniform vec4 ambientProduct, diffuseProduct, specularProduct;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform float shininess;
uniform mat3 normalMatrix;

void main()
{
	// pos is vertex position in eye coordinates
	vec3 pos = (modelViewMatrix * vPosition).xyz;

	// vector from vertex position to light source
	vec3 L;

	// check for directional light
	if(lightPosition.w == 0.0)
	{ 
		L = normalize(lightPosition.xyz);
	}
	else
	{ 
		L = normalize( lightPosition.xyz - pos );
	}

	// Because the eye point the is at the orgin
	// the vector from the vertex position to the eye is
	vec3 E = -normalize( pos );

	// halfway vector
	vec3 H = normalize( L + E );

	// Transform vertex normal into eye coordinates
	vec3 N = normalize( normalMatrix*vNormal.xyz);

	// Compute terms in the illumination equation
	vec4 ambient = ambientProduct;

	float Kd = max( dot(L, N), 0.0 );
	vec4  diffuse = Kd*diffuseProduct;

	float Ks = pow( max(dot(N, H), 0.0), shininess );
	vec4  specular = Ks * specularProduct;

	if( dot(L, N) < 0.0 ){
		specular = vec4(0.0, 0.0, 0.0, 1.0);
	} 

	gl_Position = projectionMatrix * modelViewMatrix * vPosition;

	fColor = ambient + diffuse +specular;

	fColor.a = 1.0;
}
		</textarea>
		<p id="code_vert_error" style="width: 200px;"></p>
		</td>

		<td>Fragment shader: <br>
		<textarea cols="50" rows="20" class="code_input" id="code_frag" wrap="logical">
precision mediump float;
varying vec4 fColor;

void main()
{
	gl_FragColor = fColor;
}
		</textarea>
		<p id="code_frag_error" style="width: 200px;"></p>
		</td>
	    </tr>
	  	</table>
	</table>
	<textarea id="vshader-1" style="display:none;">	
attribute vec4 vPosition;
attribute vec4 vNormal;

varying vec4 fColor;

uniform vec4 ambientProduct, diffuseProduct, specularProduct;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform float shininess;
uniform mat3 normalMatrix;

void main()
{
	// pos is vertex position in eye coordinates
	vec3 pos = (modelViewMatrix * vPosition).xyz;

	// vector from vertex position to light source
	vec3 L;

	// check for directional light
	if(lightPosition.w == 0.0)
	{ 
		L = normalize(lightPosition.xyz);
	}
	else
	{ 
		L = normalize( lightPosition.xyz - pos );
	}

	// Because the eye point the is at the orgin
	// the vector from the vertex position to the eye is
	vec3 E = -normalize( pos );

	// halfway vector
	vec3 H = normalize( L + E );

	// Transform vertex normal into eye coordinates
	vec3 N = normalize( normalMatrix*vNormal.xyz);

	// Compute terms in the illumination equation
	vec4 ambient = ambientProduct;

	float Kd = max( dot(L, N), 0.0 );
	vec4  diffuse = Kd*diffuseProduct;

	float Ks = pow( max(dot(N, H), 0.0), shininess );
	vec4  specular = Ks * specularProduct;

	if( dot(L, N) < 0.0 ){
		specular = vec4(0.0, 0.0, 0.0, 1.0);
	} 

	gl_Position = projectionMatrix * modelViewMatrix * vPosition;

	fColor = ambient + diffuse +specular;

	fColor.a = 1.0;
}
	</textarea>
	<textarea id="fshader-1" style="display:none;">
precision mediump float;
varying vec4 fColor;

void main()
{
	gl_FragColor = fColor;
}
	</textarea>
<textarea id="vshader-2" style="display:none;">
attribute vec4 vPosition;
attribute vec4 vNormal;
varying vec3 N, L, E;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform mat3 normalMatrix;

void main()
{
	vec3 pos = (modelViewMatrix * vPosition).xyz;

	// check for directional light
	if(lightPosition.w == 0.0)
	{
		L = normalize(lightPosition.xyz);
	}
	else
	{
		L = normalize( lightPosition.xyz - pos );
	}

	E = -normalize(pos);
	N = normalize( normalMatrix*vNormal.xyz);

	gl_Position = projectionMatrix * modelViewMatrix * vPosition;
}
	</textarea>
	<textarea id="fshader-2" style="display:none;">
precision mediump float;

uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;
uniform float shininess;
varying vec3 N, L, E;

void main()
{    
	vec4 fColor;

	vec3 H = normalize( L + E );
	vec4 ambient = ambientProduct;

	float Kd = max( dot(L, N), 0.0 );
	vec4  diffuse = Kd*diffuseProduct;

	float Ks = pow( max(dot(N, H), 0.0), shininess );
	vec4  specular = Ks * specularProduct;

	if( dot(L, N) < 0.0 ) specular = vec4(0.0, 0.0, 0.0, 1.0);

	fColor = ambient + diffuse +specular;
	fColor.a = 1.0;

	gl_FragColor = fColor;
}
	</textarea>
	<textarea id="vshader-3" style="display:none;">	
attribute vec4 vPosition;
attribute vec4 vNormal;

varying vec4 fColor;

uniform vec4 ambientProduct, diffuseProduct, specularProduct;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform float shininess;
uniform mat3 normalMatrix;

void main()
{
	vec3 pos = (modelViewMatrix * vPosition).xyz;
	vec3 light = lightPosition.xyz;
	vec3 L;

	// check for directional light
	if(lightPosition.w == 0.0) 
		L = normalize(lightPosition.xyz);
	else 
		L = normalize( lightPosition.xyz - pos );

	vec3 E = -normalize( pos );
	vec3 H = normalize( L + E );

	// Transform vertex normal into eye coordinates
	vec3 N = normalize( normalMatrix*vNormal.xyz);

	// Compute terms in the illumination equation
	vec4 ambient = ambientProduct;

	float Kd = max( dot(L, N), 0.0 );
	vec4  diffuse = Kd*diffuseProduct;

	float Ks = pow( max(dot(N, H), 0.0), shininess );
	vec4  specular = Ks * specularProduct;

	if( dot(L, N) < 0.0 ) {
		specular = vec4(0.0, 0.0, 0.0, 1.0);
	} 

	gl_Position = projectionMatrix * modelViewMatrix * vPosition;

	fColor = ambient + diffuse +specular;

	fColor.a = 1.0;
}
	</textarea>
	<textarea id="fshader-3" style="display:none;">
precision mediump float;

varying vec4 fColor;

void main()
{
	gl_FragColor = fColor;
}
	</textarea>
	<textarea id="vshader-4" style="display:none;">
attribute vec4 vPosition;
attribute vec4 vNormal;
varying vec3 N, L, E;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform mat3 normalMatrix;

void main()
{
	vec3 light;
	vec3 pos = (modelViewMatrix * vPosition).xyz;

	if(lightPosition.z == 0.0)
	{
		L = normalize(lightPosition.xyz);
	}
	else
	{
		L = normalize(lightPosition).xyz - pos;
	}

	E =  -normalize(pos);
	N = normalize( normalMatrix*vNormal.xyz);
	gl_Position = projectionMatrix * modelViewMatrix * vPosition;
}
	</textarea>
	<textarea id="fshader-4" style="display:none;">
precision mediump float;

uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;
uniform float shininess;
varying vec3 N, L, E;

void main()
{    
	vec4 fColor;

	vec3 H = normalize( L + E );
	vec4 ambient = ambientProduct;

	float Kd = max( dot(L, N), 0.0 );
	vec4  diffuse = Kd*diffuseProduct;

	float Ks = pow( max(dot(N, H), 0.0), shininess );
	vec4  specular = Ks * specularProduct;

	if( dot(L, N) < 0.0 ){
		specular = vec4(0.0, 0.0, 0.0, 1.0);
	}

	fColor = ambient + diffuse +specular;
	fColor.a = 1.0;

	gl_FragColor = fColor;
}
	</textarea>
	<textarea id="vshader-5" style="display:none;">
attribute vec4 vPosition;
attribute vec4 vNormal;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat3 normalMatrix;

uniform vec4 lightPosition;

uniform float shininess;

// material
uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;

varying vec3 normalInterp;
varying vec4 fColor;

void main(){
	vec4 pos = modelViewMatrix * vPosition;
	normalInterp = normalize(normalMatrix * vNormal.xyz);

	gl_Position = projectionMatrix * pos;

	vec4 ambient = ambientProduct;

	vec3 N = normalize( normalInterp );
	vec3 L;

	if( lightPosition.w == 0.0 )
	{
		L = normalize( lightPosition.xyz );
	}
	else
	{
		L = normalize( lightPosition.xyz - pos.xyz );
	}

	float Kd = max( dot( L, N ), 0.0 );
	vec4 diffuse = Kd * diffuseProduct;

	float Ks = 0.0;
	if( Kd > 0.0 )
	{
		vec3 R = reflect( -L, N );
		vec3 E = -normalize( pos.xyz );
		float specAngle = max( dot( R, E ), 0.0 );
		Ks = pow( specAngle, shininess );
	}
	vec4 specular = Ks * specularProduct;

	fColor = ambient + diffuse + specular;
	fColor.a = 1.0;
}

	</textarea>
	<textarea id="fshader-5" stlye="display:none">
precision mediump float;

varying vec4 fColor;

void main()
{
	gl_FragColor = fColor;
}
	</textarea>
	<textarea id="vshader-6" stlye="display:none">
attribute vec4 vPosition;
attribute vec4 vNormal;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;

uniform mat3 normalMatrix;

varying vec3 normalInterp;
varying vec4 vertexPos;

void main(){
	vertexPos = modelViewMatrix * vPosition;
	normalInterp = normalize(normalMatrix * vNormal.xyz);
	gl_Position = projectionMatrix * vertexPos;
}
	</textarea>
	<textarea id="fshader-6" style="display:none">
precision mediump float;

varying vec3 normalInterp;
varying vec4 vertexPos;

uniform vec4 lightPosition;
uniform float shininess;

uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;

void main()
{
	vec3 N = normalize( normalInterp );
	vec3 L;
	if( lightPosition.w == 0.0 )
		L = normalize( lightPosition.xyz );
	else
		L = normalize( lightPosition.xyz - vertexPos.xyz );

	vec4 ambient = ambientProduct;

	float Kd = max( dot( L, N ), 0.0 );
	vec4 diffuse = Kd * diffuseProduct;

	float Ks = 0.0;

	if( Kd > 0.0 )
	{
		vec3 R = reflect( -L, N );
		vec3 V = normalize( -vertexPos.xyz );
		float speculaAngle = max( dot( R, V ), 0.0 );
		Ks = pow( speculaAngle, shininess );
	}
	vec4 specular = Ks * specularProduct;

	gl_FragColor = ambient + diffuse + specular;
}

	</textarea>
</body>
</html>